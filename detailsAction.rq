#+ summary: Returns DIVE entity details, based on a entity id
#+ endpoint: http://data.dive.beeldengeluid.nl/sparql/
#+ idField: label
#+ tags:
#+      - Dive-UI

PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>
PREFIX dive: <http://purl.org/collections/nl/dive/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?label ?description ?link ?timestamp ?type (SAMPLE(?adbpediatype) AS ?dbpediatype) (SAMPLE(?aplaceholder) AS ?placeholder) (SAMPLE(?asource) AS ?source) ?relations
WHERE {
 ?_id_iri rdfs:label ?label.
 ?_id_iri rdf:type ?type.
 FILTER(?type=sem:Actor || ?type = sem:Place || ?type = sem:Event || ?type = skos:Concept || ?type=dive:MediaObject)
 OPTIONAL { ?_id_iri dc:description|dcterms:abstract|dcterms:description ?description. }
 OPTIONAL { ?_id_iri dive:hasExternalLink ?link. FILTER(str(?link) != "") }
 OPTIONAL { ?_id_iri dive:depictedBy ?adepict. ?adepict dive:source ?asource. ?adepict dive:placeholder ?aplaceholder.}
 OPTIONAL { ?_id_iri dive:source ?asource.
            ?_id_iri dive:placeholder ?aplaceholder.}
 OPTIONAL { ?_id_iri rdf:type sem:Event.
            ?_id_iri dive:hasTimeStamp ?timestamp }
 OPTIONAL { ?_id_iri dive:dbpediaType ?adbpediatype }
 BIND("Matches query," as ?relations)
}
GROUP BY ?label ?description ?link ?timestamp ?type ?relations
LIMIT 1
