#+ summary: Returns a list of statistics
#+ endpoint: http://data.dive.beeldengeluid.nl/sparql/
#+ idField: type
#+ tags:
#+      - Dive-UI
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dive: <http://purl.org/collections/nl/dive/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?type ?count ?title ?description WHERE {
  {
    SELECT ?type (COUNT(?item) as ?count) {
      VALUES ?type {
         <http://semanticweb.cs.vu.nl/2009/11/sem/Event>
         <http://semanticweb.cs.vu.nl/2009/11/sem/Actor>
         <http://semanticweb.cs.vu.nl/2009/11/sem/Place>
         dive:MediaObject
         skos:Concept
      }
      ?item rdf:type ?type
    } GROUP BY ?type
  }
  ?type rdfs:label ?title .
  OPTIONAL { ?type (rdfs:comment|skos:definition) ?description }
}
