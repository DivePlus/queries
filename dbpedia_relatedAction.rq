#+ summary: Returns DIVE entities related to entity with entityId id
#+ endpoint: http://dbpedia.org/sparql/
#+ idField: entity
#+ pagination: 50
#+ tags:
#+      - Dive-UI
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX : <http://dbpedia.org/resource/>
PREFIX dbpedia2: <http://dbpedia.org/property/>
PREFIX dbpedia: <http://dbpedia.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?entity ?type ?source ?placeholder ?label ?timestamp ?dbpediaType
WHERE
{
  {
    {
      SELECT DISTINCT ?entity
      WHERE {
        {
          ?_id_iri <http://dbpedia.org/ontology/birthDate> ?dob .
          ?entity <http://dbpedia.org/ontology/birthDate> ?dob
          BIND("birth date" AS ?relation)
        } UNION {
          ?_id_iri <http://dbpedia.org/ontology/birthPlace> ?birthPlace .
          ?entity <http://dbpedia.org/ontology/birthPlace> ?birthPlace
          BIND("birth place" AS ?relation)
        }
      }
      LIMIT 50
    }
    FILTER(?entity != ?_id_iri)
    ?entity rdf:type <http://dbpedia.org/ontology/Person> .
    ?entity rdfs:label ?label .
    FILTER (langMatches(lang(?label),"en"))
    OPTIONAL { ?entity foaf:depiction ?placeholder  }
    OPTIONAL { ?entity <http://dbpedia.org/ontology/birthDate> ?timestamp }
    OPTIONAL { ?entity <http://dbpedia.org/ontology/birthPlace> ?source  }

    BIND( "person" as ?type )
  }
}
